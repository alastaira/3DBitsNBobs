// Parametric cable clip

width = 10.0; //width of clip
radius = 5.0; //radius of clip
thickness = 3.0; //thickness of the clip
tab_length = 10.0; //length of mounting tab
screw_diameter = 2.0; //diameter of mounting screw
rounding = 1;  //rounding radius of mounting tab corners
$fn=50;

union(){
    difference(){
    translate([-radius-tab_length-thickness,0,0])
        roundedcube([tab_length+thickness,thickness,width],false,rounding,"ymax");
    rotate([-90,0,0])
        translate([-radius-(tab_length/2)-thickness,-width/2,0])
            cylinder(h=thickness,r=screw_diameter/2,center=false);
    }
    difference(){
        cylinder(h=width,r=radius+thickness,center=false);
        cylinder(h=width,r=radius,center=false);
        translate([(-radius-thickness),(-radius-thickness)*2,0])
            cube([(radius+thickness)*2,(radius+thickness)*2,width]);
    }
}

// More information: https://danielupshaw.com/openscad-rounded-corners/

// Set to 0.01 for higher definition curves (renders slower)
$fs = 0.15;

module roundedcube(size = [1, 1, 1], center = false, radius = 0.5, apply_to = "all") {
	// If single value, convert to [x, y, z] vector
	size = (size[0] == undef) ? [size, size, size] : size;

	translate_min = radius;
	translate_xmax = size[0] - radius;
	translate_ymax = size[1] - radius;
	translate_zmax = size[2] - radius;

	diameter = radius * 2;

	obj_translate = (center == false) ?
		[0, 0, 0] : [
			-(size[0] / 2),
			-(size[1] / 2),
			-(size[2] / 2)
		];

	translate(v = obj_translate) {
		hull() {
			for (translate_x = [translate_min, translate_xmax]) {
				x_at = (translate_x == translate_min) ? "min" : "max";
				for (translate_y = [translate_min, translate_ymax]) {
					y_at = (translate_y == translate_min) ? "min" : "max";
					for (translate_z = [translate_min, translate_zmax]) {
						z_at = (translate_z == translate_min) ? "min" : "max";

						translate(v = [translate_x, translate_y, translate_z])
						if (
							(apply_to == "all") ||
							(apply_to == "xmin" && x_at == "min") || (apply_to == "xmax" && x_at == "max") ||
							(apply_to == "ymin" && y_at == "min") || (apply_to == "ymax" && y_at == "max") ||
							(apply_to == "zmin" && z_at == "min") || (apply_to == "zmax" && z_at == "max")
						) {
							sphere(r = radius);
						} else {
							rotate = 
								(apply_to == "xmin" || apply_to == "xmax" || apply_to == "x") ? [0, 90, 0] : (
								(apply_to == "ymin" || apply_to == "ymax" || apply_to == "y") ? [90, 90, 0] :
								[0, 0, 0]
							);
							rotate(a = rotate)
							cylinder(h = diameter, r = radius, center = true);
						}
					}
				}
			}
		}
	}
}